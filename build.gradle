buildscript {
    ext.versions = [
        'compile_sdk': 29,
        'min_sdk': 21,
        'java': '1.8',
        'build_tools': '29.0.3',
        'android_gradle_plugin': '4.0.0',
        'kotlin': '1.3.72',
        'maven_publish_plugin': '0.12.0',
        'androidx_activity': '1.1.0',
        'androidx_constraint_layout': '1.1.3',
        'androidx_fragment': '1.2.5',
        'androidx_lifecycle': '2.2.0',
        'androidx_navigation': '2.3.0',
        'androidx_recycler_view': '1.1.0',
        'app_compat': '1.1.0',
        'core_ktx': '1.3.0',
        'coroutines': '1.3.7',
        'material_design_components': '1.1.0',
        'moshi': '1.9.3',
        'okhttp': '4.7.2',
        'retrofit': '2.9.0',
        'ktlint': '0.37.2'
    ]

    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_gradle_plugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.vanniktech:gradle-maven-publish-plugin:${versions.maven_publish_plugin}"
    }
}

subprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    group = GROUP
    version = VERSION_NAME

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:${versions.ktlint}"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
